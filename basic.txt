1   GOSUB 10
2   GOSUB 20
3   GOSUB 50
4   END
10  CLS 3
11  LOCATE 0,0,0
12  CONSOLE ,,,1
13  RETURN
20  X% = 0
21  Y% = 0
22  DX% = 0
23  DY% = 0
24  TX% = 0
25  TY% = 0
26  XA% = 0
27  YA% = 0
28  XB% = 0
29  YB% = 0
30  XC% = 0
31  YC% = 0
32  CX% = 0
33  CY% = 0
34  SEC% = 0
35  SEC1% = 0
36  K$ = ""
37  K1$ = ""
38  DIM GameWorldType(8,10)
39  DIM GameWorldData(8,10)
40  SCORE% = 0
41  TYPE% = 0
42  DATA% = 0
43  ENEMY! = 3
44  ASTEROID! = 5
45  RETURN
50  GOSUB 400
51  WHILE INKEY$ <> "w"
52  WEND
53  CLS 3
54  GameWorldType(4,0) = 1
55  GOSUB 70
56  GOSUB 440
57  GOSUB 90
58  RETURN
70  LOCATE 0,23,0
71  COLOR 0
72  RETURN
80  CLS 1
81  LOCATE 6,23,0
82  PRINT "Score:",SCORE%
83  RETURN
90  WHILE 1
91      K1$ = INKEY$
92      IF K1$ <> "" THEN K$ = K1$
93      SEC% = VAL(RIGHT$(TIME$,2))
94      IF SEC% <> SEC1% THEN SEC1% = SEC% : GOSUB 110 : GOSUB 80 : K$ = ""
95      IF ENEMY! < 0 THEN GameWorldType(FIX(RND*8),10) = 2 : ENEMY! = RND*20 ELSE ENEMY! = ENEMY! - RND*3
96      IF ASTEROID! < 0 THEN GameWorldType(FIX(RND*8),10) = 5 : ASTEROID! = RND*15 ELSE ASTEROID! = ASTEROID! - RND*3
97  WEND
98  RETURN
110 FOR X% = 0 TO 8
111     FOR Y% = 0 TO 10
112         CurrentType% = GameWorldType(X%,Y%)
113         IF CurrentType% = 3 THEN GOSUB 310
114         IF CurrentType% = 4 THEN GOSUB 370
115         IF CurrentType% = 5 THEN GOSUB 270
116         IF CurrentType% = 1 THEN GOSUB 200
117         IF CurrentType% = 2 THEN GOSUB 280
118         IF CurrentType% = 6 THEN GOSUB 300
119     NEXT Y%
120 NEXT X%
121 LINE (0,0)-(640,174),0,BF
122 FOR X% = 0 TO 8
123     FOR Y% = 0 TO 10
124         CurrentType% = GameWorldType(X%,Y%)
125         DX% = (X%+1)*64
126         DY% = 155-Y%*19
127         IF CurrentType% = 1 THEN GOSUB 460
128         IF CurrentType% = 2 THEN GOSUB 480
129         IF CurrentType% = 5 THEN GOSUB 520
130         IF CurrentType% = 3 THEN GOSUB 500
131         IF CurrentType% = 4 THEN GOSUB 510
132         IF CurrentType% = 6 THEN GOSUB 530
133     NEXT Y%
134 NEXT X%
135 RETURN
200 IF K$ = "a" AND X% > 0 THEN GOSUB 210
201 IF K$ = "d" AND X% < 8 THEN GOSUB 230
202 IF K$ = "w" THEN GOSUB 240
203 K$ = ""
204 RETURN
210 XA% = X%
211 YA% = Y%
212 XB% = X%-1
213 YB% = Y%
214 GOSUB 550
215 X% = X%-1
216 RETURN
230 XA% = X%
231 YA% = Y%
232 XB% = X%+1
233 YB% = Y%
234 GOSUB 550
235 X% = X%+1
236 RETURN
240 IF GameWorldType(X%,Y%+1) <> 0 AND GameWorldType(X%,Y%+1) <> 4 AND GameWorldType(X%,Y%+1) <> 6 THEN GameWorldType(X%,Y%+1) = 6 : GameWorldData(X,Y%+1) = 3 : RETURN
241 GameWorldType(X%,Y%+1) = 3
242 BEEP
243 RETURN
250 IF Y% > 0 THEN GOSUB 260
251 IF Y% = 0 THEN GOSUB 540
252 RETURN
260 IF GameWorldType(X%,Y%-1) = 1 THEN GOSUB 540
261 XA% = X%
262 YA% = Y%
263 XB% = X%
264 YB% = Y%-1
265 GOSUB 550
266 Y% = Y%-1
267 RETURN
270 GOSUB 250
271 RETURN
280 GOSUB 250
281 IF GameWorldData(X%,Y%) = 0 THEN GOSUB 290 ELSE GameWorldData(X%,Y%) = GameWorldData(X%,Y%)-1
282 RETURN
290 IF GameWorldType(X%,Y%-1) = 1 THEN BEEP : GOSUB 540
291 IF GameWorldType(X%,Y%-1) <> 0 AND GameWorldType(X%,Y%-1) <> 6 THEN RETURN
292 GameWorldType(X%,Y%-1) = 4
293 BEEP
294 GameWorldData(X%,Y%) = FIX(RND*10)
295 RETURN
300 IF GameWorldData(X%,Y%) < 1 THEN GameWorldType(X%,Y%) = 0 ELSE GameWorldData(X%,Y%) = GameWorldData(X%,Y%) - 1
301 RETURN
310 GOSUB 350
311 IF Y%+2 > 10 THEN RETURN
312 IF GameWorldType(X%,Y%+2) = 2 OR GameWorldType(X%,Y%+2) = 5 THEN CX% = X% : CY% = Y%+2 : GOSUB 330
313 IF GameWorldType(X%,Y%+1) = 2 OR GameWorldType(X%,Y%+1) = 5 THEN CX% = X% : CY% = Y%+1 : GOSUB 330
314 IF GameWorldType(X%,Y%-1) = 2 OR GameWorldType(X%,Y%-1) = 5 THEN CX% = X% : CY% = Y%-1 : GOSUB 330
315 IF GameWorldType(X%,Y%+2) = 4 THEN GameWorldType(X%,Y%+2) = 0
316 IF GameWorldType(X%,Y%+1) = 4 THEN GameWorldType(X%,Y%+1) = 0
317 IF GameWorldType(X%,Y%-1) = 4 THEN GameWorldType(X%,Y%-1) = 0
318 RETURN
330 IF GameWorldType(CX%,CY%) = 2 THEN SCORE% = SCORE%+10 ELSE SCORE% = SCORE%+5
331 XC% = X%
332 YC% = Y%
333 GOSUB 560
334 GameWorldType(CX%,CY%) = 6
335 GameWorldData(CX%,CY%) = 3
336 RETURN
350 IF Y%+2 > 10 THEN XC% = X% : YC% = Y% : GOSUB 560 : RETURN
351 XA% = X%
352 YA% = Y%
353 XB% = X%
354 YB% = Y%+2
355 GOSUB 550
356 Y% = Y%+2
357 RETURN
370 IF Y%-2 < 0 THEN XC% = X% : YC% = Y% : GOSUB 560 : RETURN
371 IF GameWorldType(X%,Y%-2) = 1 THEN CX% = X% : CY% = Y%-2 : GOSUB 540
372 IF GameWorldType(X%,Y%-1) = 1 THEN CX% = X% : CY% = Y%-1 : GOSUB 540
373 IF GameWorldType(X%,Y%+1) = 1 THEN CX% = X% : CY% = Y%+1 : GOSUB 540
374 GOSUB 380
375 RETURN
380 XA% = X%
381 YA% = Y%
382 XB% = X%
383 YB% = Y%-2
384 GOSUB 550
385 Y% = Y%-2
386 RETURN
400 CIRCLE (200,70),50,7
401 CIRCLE (200,70),75,7
402 LINE (220,70)-(240,115),0,BF
403 LINE (220,70)-(240,115),7,B
404 LINE (220,70)-(240,115),7
405 LINE (220,94)-(220,106),0
406 LINE (240,85)-(240,102),0
407 LINE (275,30)-(425,40),7,B
408 LINE (300,30)-(320,100),7,B
409 LINE (301,40)-(319,40),0
410 LINE (300,31)-(300,39),0
411 LINE (320,31)-(320,39),0
412 LINE (340,50)-(360,50),7
413 LINE (430,50)-(450,50),7
414 LINE (430,50)-(300,130),7
415 LINE (450,50)-(320,130),7
416 LINE (360,50)-(395,72),7
417 LINE (340,50)-(385,78),7
418 LINE (300,130)-(320,130),7
419 LOCATE 20,15
420 PRINT "No. 1!"
421 LOCATE 32,17
422 PRINT "Press W to Start"
423 RETURN
440 LINE (0,175)-(640,200),7,BF
441 LINE (180,180)-(320,190),5,BF
442 CIRCLE (337,184),14,2
443 PAINT (337,184),2
444 CIRCLE (623,184),14,2
445 PAINT (623,184),2
446 LINE (337,177)-(623,191),1,BF
447 RETURN
460 LINE (DX%,DY%+5)-(DX%+10,DY%+15),7
461 LINE (DX%,DY%+5)-(DX%-10,DY%+15),7
462 LINE (DX%+10,DY%+15)-(DX%+30,DY%+15),7
463 LINE (DX%-10,DY%+15)-(DX%-30,DY%+15),7
464 LINE (DX%,DY%-15)-(DX%+30,DY%+15),7
465 LINE (DX%,DY%-15)-(DX%-30,DY%+15),7
466 LINE (DX%+10,DY%+15)-(DX%+10,DY%-5),7
467 LINE (DX%-10,DY%+15)-(DX%-10,DY%-5),7
468 PAINT (DX%,DY%),7
469 LINE (DX%+10,DY%+15)-(DX%,DY%-15),5
470 LINE (DX%-10,DY%+15)-(DX%,DY%-15),5
471 LINE (DX%,DY%+5)-(DX%+10,DY%+15),5
472 LINE (DX%,DY%+5)-(DX%-10,DY%+15),5
473 PAINT (DX%,DY%),5
474 RETURN
480 CIRCLE (DX%,DY%),10,5
481 IF DY% > 0 THEN PAINT (DX%,DY%),5
482 LINE (DX%+10,DY%)-(DX%+20,DY%-15),2
483 LINE (DX%-10,DY%)-(DX%-20,DY%-15),2
484 LINE (DX%+10,DY%)-(DX%+30,DY%-15),6
485 LINE (DX%-10,DY%)-(DX%-30,DY%-15),6
486 LINE (DX%+10,DY%)-(DX%+40,DY%-15),7
487 LINE (DX%-10,DY%)-(DX%-40,DY%-15),7
488 LINE (DX%+40,DY%-15)-(DX%,DY%+20),7
489 LINE (DX%-40,DY%-15)-(DX%,DY%+20),7
490 CIRCLE (DX%,DY%),11,7
491 IF DY%+15 > 0 THEN PAINT (DX%,DY%+15),7
492 CIRCLE (DX%,DY%),11,0
493 LINE (DX%,DY%+5)-(DX%,DY%+20),0
494 RETURN
500 LINE (DX%+4,DY%+5)-(DX%-4,DY%-5),5,BF
501 CIRCLE (DX%,DY%+5),4,7
502 IF DY%+5 > 0 THEN PAINT (DX%,DY%+5),7
503 CIRCLE (DX%,DY%-5),4,7
504 IF DY%-5 > 0 THEN PAINT (DX%,DY%-5),7
505 RETURN
510 LINE (DX%+4,DY%+5)-(DX%-4,DY%-5),2,BF
511 CIRCLE (DX%,DY%+5),4,7
512 IF DY%+5 > 0 THEN PAINT (DX%,DY%+5),7
513 CIRCLE (DX%,DY%-5),4,7
514 IF DY%-5 > 0 THEN PAINT (DX%,DY%-5),7
515 RETURN
520 CIRCLE (DX%,DY%-3),35,2
521 IF DY% > 0 THEN PAINT (DX%,DY%),2
522 CIRCLE (DX%,DY%),30,7
523 IF DY% > 0 THEN PAINT (DX%,DY%),7
524 CIRCLE (DX%-14,DY%-8),15,0
525 IF DY%-8 > 0 THEN PAINT (DX%-14,DY%-8),0
526 CIRCLE (DX%+12,DY%+6),10,0
527 RETURN
530 CIRCLE (DX%,DY%),35,2
531 IF DY% > 0 THEN PAINT (DX%,DY%),2
532 FOR I%=0 TO FIX(RND*10)
533     TX% = DX%+(RND*35)-17.5
534     TY% = DY%+(RND*35)-17.5
535     CIRCLE(TX%,TY%),RND*10,6
536     IF TY% > 0 THEN PAINT (TX%,TY%),6
537 NEXT I%
538 RETURN
540 BEEP
541 CLS 3
542 CONSOLE ,,,0
543 LOCATE 0,0,1
544 PRINT "Game End"
545 PRINT "Score:",SCORE%
546 END
550 TYPE% = GameWorldType(XA%,YA%)
551 INFO% = GameWorldData(XA%,YA%)
552 GameWorldType(XB%,YB%) = TYPE%
553 GameWorldData(XB%,YB%) = INFO%
554 XC% = XA%
555 YC% = YA%
556 GOSUB 560
557 RETURN
560 GameWorldType(XC%,YC%) = 0
561 GameWorldData(XC%,YC%) = 0
562 RETURN